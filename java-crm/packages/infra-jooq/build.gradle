plugins {
    id 'java-library'
    id 'nu.studer.jooq' version '10.1.1'
}

group = 'com.eduplatform.packages.infra.jooq'

// Add generated source to sourceSets
sourceSets {
    main {
        java {
            srcDir "${project.projectDir}/build/generated-src/jooq/main"
        }
    }
}

dependencies {
    implementation project(':packages:common')
    
    api libs.jooq
    api libs.hikari
    api libs.postgresql
    
    // Vert.x Reactive DB Client
    api libs.bundles.vertx.db
    
    implementation 'org.springframework:spring-context'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    
    // jOOQ Code Generation
    jooqGenerator libs.postgresql
    jooqGenerator libs.jooq.meta
    jooqGenerator libs.jooq.codegen
}

// jOOQ Code Generation Configuration
jooq {
    version = '3.19.29'
    edition = nu.studer.gradle.jooq.JooqEdition.OSS

    configurations {
        main {
            generateSchemaSourceOnCompilation = false  // Manual generation

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.INFO
                
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://localhost:5432/edu_crm_db'
                    user = 'root'
                    password = '123456'
                }

                generator {
                    name = 'org.jooq.codegen.JavaGenerator'

                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        
                        // Include all tables
                        includes = '.*'
                        
                        // Exclude Flyway tables
                        excludes = 'flyway_.*'
                    }

                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = false
                        fluentSetters = true
                        javaTimeTypes = true
                        springAnnotations = false  // Không cần, đã viết repo thủ công
                        springDao = false          // Không dùng generated DAO
                        pojos = true
                        pojosEqualsAndHashCode = true
                        pojosToString = true
                        daos = false               // Không dùng generated DAO  
                        routines = false
                    }

                    target {
                        packageName = 'com.eduplatform.jooq.generated'
                        directory = "${project.projectDir}/build/generated-src/jooq/main"
                    }

                    strategy {
                        name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                    }
                }
            }
        }
    }
}

// Task to generate jOOQ code
tasks.named('generateJooq').configure {
    doFirst {
        println "Generating jOOQ code from database..."
    }
    doLast {
        println "jOOQ code generation completed!"
    }
}
