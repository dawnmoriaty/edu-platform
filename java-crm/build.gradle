plugins {
    alias(libs.plugins.spring.boot) apply false
    alias(libs.plugins.spring.dependency)
    alias(libs.plugins.dotenv)
}

allprojects {
    group = 'com.eduplatform'
    version = '1.0.0-SNAPSHOT'

    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom libs.boms.spring.get().toString()
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs.add('-parameters')
    }

    tasks.withType(Test) {
        useJUnitPlatform()
    }

    dependencies {
        compileOnly libs.lombok
        annotationProcessor libs.lombok
        
        implementation libs.jakarta.annotation
    }
}
